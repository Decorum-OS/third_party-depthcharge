ENTRY(_entry)

SECTIONS
{
	. = 0;
	ImageBase = .;

	. = 0x2000;

	. = ALIGN(4096);
	.text : {
		_start = .;
		*(.text._entry)
		*(.text)
		*(.text.*)
		. = ALIGN(16);
	}

	. = ALIGN(4096);
	.reloc : {
		KEEP(*(.reloc))
	}

	. = ALIGN(4096);
	.data :	{
		_init_funcs_start = .;
		KEEP(*(SORT_BY_NAME(.init_funcs.*)))
		_init_funcs_end = .;
		_reloc_start = .;
		KEEP(*(.rela*))
		_reloc_end = .;
		*(.rodata*)
		*(.plt)
		*(.got.plt)
		*(.got)
		*(.data*)
	}

	.dynsym : {
		_dynsym_start = .;
		KEEP(*(.dynsym))
		_dynsym_end = .;
	}

	.bss : {
		*(.bss*)
		_end = .;
	}

	/* Throw away everything we didn't list explicitly above. */
	/DISCARD/ : {
		*(*)
	}
}
