OUTPUT_FORMAT("elf64-x86-64", "elf64-x86-64", "elf64-x86-64")
OUTPUT_ARCH(i386:x86-64)
ENTRY(_entry)

SECTIONS
{
	. = 0;
	ImageBase = .;

	/* This MUST come first! */
	.hash :  {
		*(.hash)
	}

	. = ALIGN(4096);
	.eh_frame : { 
		_start = .;
		*(.eh_frame)
	}

	. = ALIGN(4096);
	.text : {
		_text = .;
		*(.text)
		*(.text.*)
		*(.gnu.linkonce.t.*)
		. = ALIGN(16);
	}
	_etext = .;
	_text_size = . - _text;

	. = ALIGN(4096);
	.reloc : {
		KEEP(*(.reloc))
	}

	. = ALIGN(4096);
	.data :	{
		_data = .;
		*(.rodata*)
		*(.plt)
		*(.got.plt)
		*(.got)
		*(.data*)
		*(.sdata)
		/*
		 * The EFI loader doesn't seem to like a .bss section, so we
		 * stick it all into .data:
		 */
		*(.sbss)
		*(.scommon)
		*(.dynbss)
		*(.bss*)
		*(COMMON)
		_init_funcs_start = .;
		KEEP(*(SORT_BY_NAME(.init_funcs.*)))
		_init_funcs_end = .;
		*(.rel.local)
	}
	_edata = .;
	_data_size = . - _etext;

	. = ALIGN(4096);
	.dynamic : {
		*(.dynamic)
	}

	. = ALIGN(4096);
	.rela : {
		*(.rela.data*)
		*(.rela.got)
		*(.rela.plt)
		*(.rela.stab)
		*(.rela.init_funcs*)
	}

	. = ALIGN(4096);
	.dynsym : {
		*(.dynsym)
	}

	. = ALIGN(4096);
	.dynstr : {
		*(.dynstr)
	}

	. = ALIGN(4096);
	.ignored.reloc : {
		*(.rela.reloc)
		*(.eh_frame)
		*(.note.GNU-stack)
		_end = .;
	}

	/DISCARD/ : {
		*(.comment)
		*(.note*)
	}
}
